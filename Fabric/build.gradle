plugins {
  id 'fabric-loom' version '0.12-SNAPSHOT'
  id 'maven-publish'
  id 'idea'
  id 'com.matthewprenger.cursegradle' version '1.4.0'
  id 'nu.studer.credentials' version '1.0.7'
  id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_id}-fabric-${minecraft_version}"

repositories {
  maven {
    name = "Fuzs Mod Resources"
    url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
  }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings loom.officialMojangMappings()
  modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
  implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
  implementation project(":Common")

  // Forge Config Support
  implementation 'com.electronwill.night-config:core:3.6.3'
  implementation 'com.electronwill.night-config:toml:3.6.3'
  modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0"

  // Recommended Mods
  modImplementation "curse.maven:tooltipfix-411557:4577194"

  // Additional helpful Mods for development
  modImplementation "curse.maven:jei-238222:4712867"
}

loom {
  runs {
    client {
      client()
      setConfigName("Fabric Client")
      ideConfigGenerated(true)
      runDir("run")
    }
    server {
      server()
      setConfigName("Fabric Server")
      ideConfigGenerated(true)
      runDir("run")
    }
  }
}


processResources {
  from project(":Common").sourceSets.main.resources
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) {
  source(project(":Common").sourceSets.main.allSource)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.archivesBaseName
      version project.version
      from components.java
    }
  }

  repositories {
    maven {
      url "file://" + System.getenv("local_maven")
    }
  }
}

curseforge {
  apiKey = "${credentials.curseforge_api_token}"
  project {
    id = "${curseforge_project_id}"
    changelog = file("../CHANGELOG.md")
    changelogType = 'markdown'
    releaseType = 'release'
    addGameVersion("${minecraft_version}")
    addGameVersion("Fabric")
    addGameVersion("Quilt")

    mainArtifact(remapJar) {
      relations {
        requiredDependency 'fabric-api'
        requiredDependency 'forge-config-api-port-fabric'
        optionalDependency 'tooltipfix'
      }
    }
  }
  options {
    forgeGradleIntegration = false
  }
}

modrinth {
  token = "${credentials.modrinth_api_token}"
  projectId = "${modrinth_project_id}"
  versionNumber = "${version}"
  versionType = "${modrinth_release_type}"
  uploadFile = remapJar
  gameVersions = ["${minecraft_version}"]
  loaders = ["fabric", "quilt"]
  dependencies {
    required.project "fabric-api"
    required.project "forge-config-api-port"
    optional.project "tooltipfix"
  }
}
